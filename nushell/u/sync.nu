const MERGE_EXTS = [c conf cpp css go html js json json5 jsonc kdl lua md nu py rs scss sql surql svelte toml ts tsx txt zig]
const BACKUP_EXTS = [md surql kdbx]

const MERGE_STR = ("*.{" ++ ($MERGE_EXTS | str join ",") ++ "}" | str join)
const BACKUP_STR = ("*.{" ++ ($BACKUP_EXTS | str join ",") ++ "}" | str join)

const u0prf = $"# Generated by Nushell UA
backup = Name {($BACKUP_STR)}
backupcurr = Name {($BACKUP_STR)}
backuploc = central
# backupdir = /w/...trash
backupprefix = ...trash-
backupsuffix = .$VERSION
maxbackups = 9

sshargs = -C

auto = true
batch = true

fastcheck = true
fastercheckUNSAFE = false

ignore = Name ..*
ignore = Name .git/*
ignorenot = Name .git/config
ignore = Name .swc/*
ignore = Name *.tsbuildinfo
ignore = Name .DS_Store
ignore = Name Thumbs.db
ignore = Name __pycache__
ignore = Name .vitepress/cache
ignore = Name src-tauri/target
ignore = Name src-tauri/gen
ignore = Name node_modules
ignore = Name .crush
ignore = Name .svelte-kit
ignore = Regex ^.*/_big/.*
ignore = Name bun.lock*
ignore = Name vite.config.[jt]s.timestamp-*

ignore = Path c/*
ignorenot = Path c/??

ignore = Path d/*/{archive,*.log,LOG*,LOCK}

ignore = Path g/*/{*,.*}
ignorenot = Path g/*/.git
ignorenot = Path g/*/.git/config
ignorenot = Path g/vdev/*
ignore = Path g/vdev/*/{*,.*}
ignorenot = Path g/vdev/*/.git
ignorenot = Path g/vdev/*/.git/config

times = true
diff = delta OLDER NEWER
merge = Name {($MERGE_STR)} -> diff3 -m CURRENT1 CURRENTARCH CURRENT2 > NEW

owner = true
group = true
"

use ($nu.default-config-dir + "/u/confirm-if.nu")
alias cif = confirm-if

# https://github.com/nushell/nushell/discussions/13999#discussioncomment-10838086
def "nu-complete files" [
	prefix: string
] { 
	let word = $prefix | split row -r '\s+' | last
	glob $"($word)*" | path relative-to $env.PWD 
}

export def main [
    args: list<string> = []
    --confirm (-c)
    --generate (-g)
	--user (-u)
    --dir (-d): string@"nu-complete files"
] {
	let $dir = ($dir | default (open ($nu.default-config-dir)/.u.nuon | get SYNC.DEFAULT_DIR) | path expand)
	let REMOTE_LIST = (open ($nu.default-config-dir)/.u.nuon | get SYNC.REMOTE_LIST)
	# assert device-specific config

    if $generate {
		sudo mkdir -p /root/.unison
        $u0prf | sudo tee /root/.unison/ua.prf out> /dev/null
		mkdir $"($nu.home-dir)/.unison"
        $u0prf | save -f $"($nu.home-dir)/.unison/ua.prf"
        return
    }

    let remote = ($REMOTE_LIST | input list -f "Choose your VPS")
    if ($remote | is-empty) {
        return
    }

	let $username = if not $user {"root"} else {(whoami)}

    let $local_path = $dir
    let $remote_path = ([$"ssh://($username)@($remote.ip):($remote.port)/" $dir] | str join)

    let hosts = [$local_path $remote_path]

    cif $confirm [$hosts]

    let cmds = ["unison" "ua" ...$hosts $"-clientHostName=($env.HOSTNAME)" "-batch=false" "-auto=false" ...$args]
	if ($user) {
		print ...$cmds
		run-external ...$cmds
	} else {
		print [sudo ...$cmds]
		^sudo ...$cmds
	}
}
